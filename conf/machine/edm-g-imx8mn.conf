#@TYPE: Machine
#@NAME: EDM-G-IMX8M
#@SOC: i.MX8MN
#@DESCRIPTION: Machine configuration for Technexion EDM-G-IMX8MN
#@MAINTAINER: Andy Lin <andy.lin@technexion.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mn:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc
require conf/machine/tn-base.inc

# Kernel specifics
KERNEL_DEVICETREE ?= "freescale/imx8mn-edm-g-wb.dtb \
		      freescale/imx8mn-edm-g-wb-rpmsg.dtb \
		      freescale/overlays/imx8mn-edm-g-wb-tevi-ov5640.dtbo \
		      freescale/overlays/imx8mn-edm-g-wb-sn65dsi84-vl10112880.dtbo \
		      freescale/overlays/imx8mn-edm-g-wb-sn65dsi84-vl15613676.dtbo \
		      freescale/overlays/imx8mn-edm-g-wb-sn65dsi84-vl215192108.dtbo \
"

EXTRA_OEMAKE_append_pn-linux-tn-imx = " DTC_FLAGS='-@ -H epapr'"

# U-boot specifics
UBOOT_CONFIG = "sd"
UBOOT_CONFIG[sd] = "edm-g-imx8mn_defconfig,sdcard"
UBOOT_CONFIG[mfgtool] = "edm-g-imx8mn_defconfig"
UBOOT_DTB_NAME = "imx8mn-edm-g.dtb"

IMX_BOOT_SEEK_mx8mn = "32"
SERIAL_CONSOLES = "115200;ttymxc1"
SERIAL_BLUETOOTH = "3000000;ttymxc2"
BOOT_SPACE = "65536"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = "lpddr4_pmu_train_1d_imem.bin lpddr4_pmu_train_1d_dmem.bin lpddr4_pmu_train_2d_imem.bin lpddr4_pmu_train_2d_dmem.bin"

#WKS_FILE_DEPENDS_append = " imx-m4-demos-tn"
#M4_MACHINE = "edm-g-imx8mn"
#IMAGE_BOOT_FILES += "hello_world.bin rpmsg_lite_pingpong_rtos_linux_remote.bin rpmsg_lite_str_echo_rtos_imxcm7.bin"

# WIC Image
WKS_FILE_virtualization = "tn-imx8-imxboot-rootfs-container.wks.in"
WIC_FSTAB_BLKDEV = "mmcblk2"
WIC_CREATE_EXTRA_ARGS_remove_virtualization = "--no-fstab-update"
WIC_PARTITION_TABLE_TYPE ?= "msdos"
IMAGE_FSTYPES_append_virtualization = " wic wic.xz wic.md5sum"
IMAGE_FSTYPES_remove_virtualization = " sdcard sdcard.bz2 sdcard.xz sdcard.md5sum"

IMAGE_BOOTLOADER = "imx-boot"
LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""

# virtualization - docker containers
DOCKERDISKDEPENDS = "${@bb.utils.contains('DISTRO_FEATURES', 'virtualization', 'docker-disk:do_deploy', '', d)}"
do_image_wic[depends] += "${DOCKERDISKDEPENDS}"
TN_DOCKER_PARTITION_IMAGE = "docker-data-partition.img"
TN_DOCKER_PARTITION_MOUNT = "/var/lib/docker"
TN_DOCKER_PARTITION_SIZE = "4096"

# UBOOT entry address for fitImages
UBOOT_ENTRYPOINT_rescue = "0x40480000"
#UBOOT_RD_ENTRYPOINT_rescue = "0x43800000"
#UBOOT_RD_LOADADDRESS_rescue = "0x43800000"
UBOOT_DTB_LOADADDRESS_rescue = "0x40430000"
UBOOT_DTBO_LOADADDRESS_rescue = "0x40420000"
UBOOT_FIT_LOADADDRESS_rescue = "0x45800000"
UBOOT_FIT_PREFIX_rescue = "freescale_"
